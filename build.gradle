plugins {
    id 'org.springframework.boot' version '3.2.3' apply false
    id 'io.spring.dependency-management' version '1.1.5'
    id "com.diffplug.spotless" version "6.25.0"
    id 'java'
    id "jacoco"
    id "org.sonarqube" version "7.0.1.6134"
}

allprojects {
    group = 'com.innowise'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.2.3"
        }
    }

    dependencies {
        implementation 'me.paulschwarz:spring-dotenv:4.0.0'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'io.micrometer:micrometer-registry-prometheus'
    }

    spotless {
        java { googleJavaFormat() }
    }

    tasks.test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }
    }
}

jacoco {
    toolVersion = "0.8.11"
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn subprojects.test
    dependsOn subprojects.jacocoTestReport

    additionalSourceDirs.setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    sourceDirectories.setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    classDirectories.setFrom(files(subprojects.sourceSets.main.output))
    executionData.setFrom(files(subprojects.jacocoTestReport.executionData))

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/*Application*',
                    '**/config/**',
                    '**/entity/**',
                    '**/dto/**',
                    '**/exception/**'
            ])
        }))
    }
}

sonar {
    properties {
        property "sonar.projectKey", "YauhenNohach_innowise_internship"
        property "sonar.organization", "yauhennohach"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.token", System.getenv("SONAR_TOKEN") ?: ""
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.rootDir}/build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
        property "sonar.java.binaries", "${project.rootDir}/**/build/classes/java/main"
        property "sonar.java.test.binaries", "${project.rootDir}/**/build/classes/java/test"
        property "sonar.sources", "${project.rootDir}/**/src/main/java"
        property "sonar.tests", "${project.rootDir}/**/src/test/java"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.junit.reportPaths", "${project.rootDir}/**/build/test-results/test"
        property "sonar.coverage.exclusions", "**/*Application.java,**/config/**,**/entity/**,**/dto/**,**/exception/**"
        property "sonar.test.exclusions", "**/test/**"
    }
}

tasks.sonar {
    dependsOn jacocoRootReport
}